const stdin 0 end
const stdout 1 end
const stderr 2 end

const EXIT_SUCCESS 0 end
const EXIT_FAILURE 1 end

const SPACE 32 end

macro NULL 0 cast(ptr) end

proc @ptr ptr -- ptr in @32 cast(ptr) end
proc !ptr ptr ptr -- in cast(int) !32 end

proc @bool ptr -- bool in @8 cast(bool) end
proc !bool ptr bool -- in cast(int) !8 end

macro @int @32 end
macro !int !32 end

macro ptr_diff
  swap cast(int)
  swap cast(int)
  -
end

proc strlen ptr -- int in
  dup
  while dup @8 0 = not do 1 + end
  swap ptr_diff
end

proc write int ptr int -- int in
	c_call(3) write
end

proc malloc int -- ptr in
	c_call(1) malloc cast(ptr)
end

proc fputs ptr int -- in
	// excepts string on the stack
	// [ptr:string data, int:file descriptor]
	// fd len data fd
	swap dup strlen
	swap rot write drop drop
end

proc puts ptr -- in stdout fputs end
proc eputs ptr -- in stderr fputs end

macro endl "\n" end

macro malloc4 4 malloc end

macro malloc2 2 malloc end

macro malloc1 1 malloc end

proc inc32 ptr -- in dup @int 1 + !int end
proc dec32 ptr -- in dup @int 1 - !int end

proc ptr+ ptr int -- in
	swap dup @int rot
	+ !int drop
end

proc ptr- ptr int -- in
	swap dup @int rot
	- !int drop
end

macro todo!
	"TODO\n" puts
	EXIT_FAILURE exit
end

const sizeof(int)  4 end
const sizeof(bool) 4 end
const sizeof(ptr)  4 end

proc cstr_to_str ptr -- int ptr in
	// ins [ptr] : outs[int, ptr]
	dup strlen swap
end

proc puti int -- in print endl puts end


// struct Str

const Str.count 0 end
const Str.data sizeof(ptr) end
const sizeof(Str) Str.data 4 + end

memory str_len 4 end
memory str_data 4 end

proc !Str ptr ptr -- in
	// ins [ptr, ptr] : outs [ptr]
	swap cstr_to_str
	str_data swap !ptr
	str_len swap !int
	dup str_len @int !int
	dup Str.data + str_data
	@ptr !ptr drop
end

proc @Str.count ptr -- int in
	// ins [Str] : outs [int]
	// reads first 4 bytes of struct
	// where stored size
	@int
end

proc @Str.data ptr -- ptr in
	// ins [Str] : outs [ptr]
	// reads data field of Str struct
	Str.data + @ptr
end

proc @Str ptr -- int ptr in
	dup @Str.count
	swap @Str.data
end

proc !empty_Str ptr -- ptr in
	// ins [ptr] : outs [Str]
	dup 0 !int
	dup Str.data + NULL !ptr
end

proc str_cut ptr -- ptr in
	// ins [Str] : outs [Str]
	// Str
	dup dec32
	dup Str.data + inc32
end

// end of struct Str